/*
This is the gradle build file for the entire project.
Its main job is setting up Minecraft dependencies, some java details, and the subprojects (common/forge/fabric)

Anything that applies to all the subprojects should go here.

You probably won't really need to touch too much in here unless you need to depend on another mod/library
or mess with build stuff but I wouldn't recommend doing that until you're a bit more familiar with everything.
*/

// add our gradle plugins
plugins {
    // NOTE: these architectury bits are different from architectury API
    id 'dev.architectury.loom' version '1.6-SNAPSHOT' apply false // arch loom is a fork of fabric loom. it deals with deobfuscating the mc code for fabric/forge 
    id 'architectury-plugin' version '3.4-SNAPSHOT' // deals with making the common module stuff work
    id 'com.github.johnrengelman.shadow' version '8.1.1' apply false // honestly not 100% sure what this does, something with the multiloader setup
}

// tell architectury what minecraft version we're using
architectury {
    minecraft = project.minecraft_version // <- references from gradle.properties
}

// sets these properties for all subprojects, so root, common, forge, and fabric
allprojects {
    // maven group is usually similar to package, so here it'd be com.quatalog.rpimod
    group = rootProject.maven_group 

    // whatever version your mod is on. usually something like 0.0.1
    // sometimes you'll see the mc version in there too like 0.0.1+1.20.1 for mods that support multiple versions
    version = rootProject.mod_version 
}

// set up the subprojects, so common, forge, fabric
subprojects {
    apply plugin: 'dev.architectury.loom'
    apply plugin: 'architectury-plugin'
    apply plugin: 'maven-publish'

    base {
        // Set up a suffixed format for the mod jar names, e.g. `example-fabric`.
        archivesName = "$rootProject.archives_name-$project.name"
    }

    repositories {
        // Add repositories to retrieve artifacts from in here.
        // You should only use this when depending on other mods because
        // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
        // See https://docs.gradle.org/current/userguide/declaring_repositories.html
        // for more information about repositories.
    }

    // depend on minecraft, pretty important
    dependencies {
        minecraft "net.minecraft:minecraft:$rootProject.minecraft_version"
        mappings "net.fabricmc:yarn:$rootProject.yarn_mappings:v2"
    }

    java {
        // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
        // if it is present.
        // If you remove this line, sources will not be generated.
        withSourcesJar()

        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.release = 17
    }

    // Configure Maven publishing - not really something you'll worry about for a while
    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId = base.archivesName.get()
                from components.java
            }
        }

        // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
        repositories {
            // Add repositories to publish to here.
            // Notice: This block does NOT have the same function as the block in the top level.
            // The repositories here will be used for publishing your artifact, not for
            // retrieving dependencies.
        }
    }
}
